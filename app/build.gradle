/*
 * Copyright 2016 Martin Kamp Jensen
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'jacoco-everywhere'

apply from: '../config/checkstyle.gradle'

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    classpath 'com.trickyandroid:jacoco-everywhere:0.2.1'
  }
}

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.3"

  defaultConfig {
    applicationId "com.github.mkjensen.dml"
    minSdkVersion 21
    targetSdkVersion 23
    testInstrumentationRunner "com.github.mkjensen.dml.test.DmlAndroidJUnitRunner"
    versionCode 1
    versionName "1.0"
  }

  signingConfigs {
    release {
      if (project.hasProperty('SIGNING_KEY_ALIAS')) {
        keyAlias SIGNING_KEY_ALIAS
        keyPassword SIGNING_KEY_PASSWORD
        storeFile file(SIGNING_STORE_FILE)
        storePassword SIGNING_STORE_PASSWORD
      }
    }
  }

  buildTypes {
    debug {
      testCoverageEnabled = true
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      if (project.hasProperty('SIGNING_KEY_ALIAS')) {
        signingConfig signingConfigs.release
      }
    }
  }

  lintOptions {
    abortOnError false
    lintConfig rootProject.file('config/lint.xml')
  }

  packagingOptions {
    // espresso-core, espresso-web, and Guava include the same files which cause a conflict.
    // https://code.google.com/p/android/issues/detail?id=195331
    exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
    exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
  }
}

dependencies {
  androidTestCompile 'com.android.support:support-annotations:23.3.0'
  androidTestCompile("com.android.support.test.espresso:espresso-contrib:2.2.2") {
    exclude group: 'com.android.support', module: 'appcompat'
    exclude group: 'com.android.support', module: 'support-v4'
    exclude group: 'com.android.support', module: 'recyclerview-v7'
  }
  androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
  androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'
  androidTestCompile 'com.android.support.test.espresso:espresso-web:2.2.2'
  androidTestCompile 'com.android.support.test:rules:0.5'
  androidTestCompile 'com.android.support.test:runner:0.5'
  androidTestCompile 'junit:junit:4.12'

  apt 'com.google.dagger:dagger-compiler:2.4'

  compile 'com.android.support:leanback-v17:23.3.0'
  compile 'com.facebook.stetho:stetho:1.3.1'
  compile 'com.facebook.stetho:stetho-okhttp3:1.3.1'
  compile 'com.github.bumptech.glide:glide:3.7.0'
  compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
  compile 'com.google.android.exoplayer:exoplayer:r1.5.7'
  compile 'com.google.dagger:dagger:2.4'
  compile 'com.squareup.okhttp3:okhttp:3.2.0'
  compile 'com.squareup.retrofit2:converter-moshi:2.0.1'
  compile 'com.squareup.retrofit2:retrofit:2.0.1'

  provided 'javax.annotation:jsr250-api:1.0'

  testCompile 'junit:junit:4.12'
}
